module optimisation/cse

imports
  strategolib
  signatures/-
  optimisation/opt-lib

strategies
  cse :: ? -> ?
  cse = 
    alltd(CSE <+ cse-rule)

  cse-rule :: ? -> ?
  cse-rule =
    Block({| CSE : map(cse) |})

  cse-rule = 
    (?Declaration(x) <+ ?DeclarationTyped(x, t))
    ; new-CSE(|x, x)

  cse-rule = 
    Assign(?x, cse => e)
    ; undefine-CSE(|x)
    ; if <not(contains(|Var(x)))> e then
        where( innermost-scope-CSE => z )
        ; where( get-var-dependencies => deps )
        ; rules( CSE.z : e -> Var(x) depends on deps )
      end

  cse-rule =
    IfElse(cse, id, id)
    ; (IfElse(id, cse, id) /CSE\ IfElse(id,id,cse))

  cse-rule =
    ?While(_, _)
    ; (/CSE\* While(cse, cse))

  innermost-scope-CSE :: ? -> ?
  innermost-scope-CSE = 
    get-var-names => vars
    ; innermost-scope-CSE(elem-of(|vars))
