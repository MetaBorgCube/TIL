module optimisation/copyprop-rev

imports
  strategolib
  signatures/-
  optimisation/opt-lib

strategies
  copyprop-rev :: ? -> ?
  copyprop-rev = 
    alltd(CopyPropRev <+ copyprop-rev-rule)

  copyprop-rev-rule :: ? -> ?
  copyprop-rev-rule =
    Block({| CopyPropRev : map(copyprop-rev) |})

  copyprop-rev-rule = 
    (?Declaration(x) <+ ?DeclarationTyped(x, t))
    ; new-CopyPropRev(|x, x)

  copyprop-rev-rule = 
    Assign(?x, copyprop-rev => e)
    ; undefine-CopyPropRev(|x)
    ; where( innermost-scope-CopyPropRev => z )
    ; if <?Var(y)> e then
        rules( CopyPropRev.z : Var(y) -> Var(x) depends on [(x,x),(y,y)] )
      end

  copyprop-rev-rule =
    IfElse(copyprop-rev, id, id)
    ; (IfElse(id, copyprop-rev, id) /CopyPropRev\ IfElse(id,id,copyprop-rev))

  copyprop-rev-rule =
    ?While(_, _)
    ; (/CopyPropRev\* While(copyprop-rev, copyprop-rev))

  innermost-scope-CopyPropRev :: ? -> ?
  innermost-scope-CopyPropRev = 
    get-var-names => vars
    ; innermost-scope-CopyPropRev(elem-of(|vars))
