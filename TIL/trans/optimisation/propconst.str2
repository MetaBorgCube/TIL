module optimisation/propconst

imports
  strategolib
  signatures/-
  optimisation/opt-lib
  rewrite/eval

strategies
  propconst =
    PropConst
    <+ propconst-rule
    <+ all(propconst); try(EvalExp)

  propconst-rule =
    Block({| PropConst : map(propconst) |})

  propconst-rule =
    (?Declaration(x) <+ ?DeclarationTyped(x, t))
    ; rules( PropConst+x :- Var(x) )

  propconst-rule =
    Assign(?x, propconst => e)
    ; if <is-value> e then
        rules( PropConst.x : Var(x) -> e )
      else
        rules( PropConst.x :- Var(x) )
      end

  propconst-rule =
    IfThen(propconst, id)
    ; (EvalIf; propconst
       <+ IfThen(id, {| PropConst: map(propconst) |}) /PropConst\ id)

  propconst-rule =
    IfElse(propconst, id, id)
    ; (EvalIf; propconst
       <+ IfElse(id, {| PropConst: map(propconst) |}, id) /PropConst\
          IfElse(id, id, {| PropConst: map(propconst) |}))

  propconst-rule =
    ?While(_, _)
    ; (/PropConst\* While(propconst, {| PropConst: map(propconst) |}))

  propconst-rule =
    Labeled(?label,
      dr-fix-and-intersect(While(propconst, {| PropConst: map(propconst) |}) | ["PropConst"], label)
    )

  propconst-rule =
    For(?x, propconst, propconst, id)
    ; rules( PropConst.x :- Var(x) )
    ; (/PropConst\* For(id, id, id, {| PropConst: map(propconst) |}))

  propconst-rule =
    Labeled(?label,
      dr-label-intersect(propconst | ["PropConst"], label)
    )

  propconst-rule =
    ?Break(None())
    ; break-PropConst

  propconst-rule =
    ?Break(Some(label))
    ; break-to-label-PropConst(|label)

  propconst-rule =
    ?Continue(None())
    ; continue-PropConst

  propconst-rule =
    ?Continue(Some(label))
    ; continue-to-label-PropConst(|label)

  propconst-rule =
    ?Try(tblock, cclauses)
    ; dr-init-exception-block(fail | <get-catch-tags>cclauses, ["PropConst"])
    ; <propconst>tblock => tblock'
    ; <map(dr-complete-catch-intersect(propconst | ["PropConst"]))>cclauses => cclauses'
    ; dr-complete-exception-block-intersect(|["PropConst"])
    ; !Try(tblock', cclauses')

  propconst-rule =
    ?Try(tblock, cclauses, fblock)
    ; dr-init-exception-block(id | <get-catch-tags>cclauses, ["PropConst"])
    ; <propconst>tblock => tblock'
    ; <map(dr-complete-catch-intersect(propconst | ["PropConst"]))>cclauses => cclauses'
    ; <dr-complete-finally-intersect(propconst | ["PropConst"])>fblock => fblock'
    ; dr-complete-exception-block-intersect(|["PropConst"])
    ; !Try(tblock', cclauses', fblock')
  get-catch-tags =
    map(?Catch(_, <id>, _))

  propconst-rule =
    ?Throw(TypeName(t))
    ; throw-PropConst(eq | t)
