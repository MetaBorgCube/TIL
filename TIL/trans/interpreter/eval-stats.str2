module interpreter/eval-stats

imports
  strategolib
  interpreter/eval-var
  interpreter/eval-exp
  rewrite/eval
  signatures/-

signature
  constructors
    Goto : string -> Stat

strategies

  eval-stat-rule(eval-read) = 
    ?Block(<eval-stats(eval-read)>)

  eval-stats(eval-read) = 
    {| EvalVar : map(
//    debug(!"eval-stats1 ");
    eval-stat(eval-read)
//    ;debug(!"eval-stats2 ")
    ) |}

  eval-stat(eval-read) =
    eval-stat-rule(eval-read)
    <+ EvalGoto(eval-read)

  eval-stat-rule(eval-read) = 
    IfElse(eval-exp(eval-read), id, id)
    ; EvalIf
    ; eval-stat(eval-read)

  eval-stat-rule(eval-read) = 
    ?While(e, st*)
    ; where(<local-newname> "while" => label)
    ; where(<conc>(st*, [Goto(label)]) => st2*)
    ; rules( EvalGoto(eval-read) : Goto(label) -> <eval-stat(eval-read)>IfElse(e, st2*, []) )
    ; <eval-stat(eval-read)> Goto(label)
